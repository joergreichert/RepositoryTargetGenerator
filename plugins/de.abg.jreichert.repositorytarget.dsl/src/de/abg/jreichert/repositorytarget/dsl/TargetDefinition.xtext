grammar de.abg.jreichert.repositorytarget.dsl.TargetDefinition hidden(WS, ML_COMMENT, SL_COMMENT)
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate targetDefinition "http://www.abg.de/jreichert/TargetDefinition"

Target:
	'TargetDefinition' name=STRING
	('targetFile' '=' targetFileName=ID)?
	('categories' '{'
		categories+=Category (',' categories+=Category)*
	'}')?	
	locations+=Location*
;

Category:
	name=ID (longName=STRING)? ('description:' description=STRING)? (default?='default')?
;

Location:
	(name=STRING)? repositoryLocation=URL ('category' '=' (categories+=[Category]|('('categories+=[Category] (',' categories+=[Category])* ')')))? (strictVersion?='strictVersion')? '{'
		(unit+=Unit) (',' unit+=Unit)*
	'}'
;

Unit:
	categoryId=ID ((noFeature?='noFeature')? & (version=VERSION)?) 
		('category' '=' (categories+=[Category]|('('categories+=[Category] (',' categories+=[Category])* ')')))? 
		(strictVersion?='strictVersion')?
;

terminal URL: 
	(('http'|'https')('://')|('file:/'))('a'..'z'|'A'..'Z'|'?'|'&'|'%'|'$'|'/'|'.'|':'|'_'|'-'|'0'..'9')*
;

terminal ID:
	('a'..'z'|'A'..'Z'|'_'|'.'|'-')('a'..'z'|'A'..'Z'|'_'|'.'|'-'|'0'..'9')*
; 

terminal VERSION:
	('0'..'9')*('.'('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9')*)*
;

terminal STRING	: 
			'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
			"'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
		; 
terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;
